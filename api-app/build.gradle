plugins {
  id 'java'
  id 'checkstyle'
  id 'pmd'
  id 'org.springframework.boot' version '3.4.2'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'com.diffplug.spotless' version '7.0.2'
}

group = 'de.muspellheim.activitysampling'
version = '0.0.1-SNAPSHOT'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.flywaydb:flyway-core'
  implementation 'org.flywaydb:flyway-database-postgresql'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  compileOnly 'org.projectlombok:lombok'
  runtimeOnly 'org.postgresql:postgresql'
  developmentOnly 'org.springframework.boot:spring-boot-devtools'
  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
  annotationProcessor 'org.projectlombok:lombok'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'com.tngtech.archunit:archunit-junit5:1.3.0'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  testRuntimeOnly 'com.h2database:h2'
}

test {
  useJUnitPlatform()
  testLogging {
    events 'passed', 'skipped', 'failed'
    showExceptions = true
    exceptionFormat 'full'
  }
}

tasks.register('processFrontendResources', Copy) {
  group = 'Frontend'
  description = 'Process frontend resources'

  from(project.layout.projectDirectory.dir('../web-app/dist'))
  into(project.layout.buildDirectory.dir('resources/main/public'))
}

tasks.named('processResources') {
  dependsOn('processFrontendResources')
}

checkstyle {
  toolVersion = '10.21.2'
  config = resources.text.fromArchiveEntry(configurations.checkstyle.find {
    it.name.contains('checkstyle')
  }, 'google_checks.xml')
  configProperties = [
    "org.checkstyle.google.suppressionfilter.config":
      "${projectDir}/config/checkstyle/suppressions.xml",
  ]
}

pmd {
  consoleOutput = true
  toolVersion = '7.10.0'
}

spotless {
  java {
    googleJavaFormat()
    licenseHeader '/* Copyright (c) $YEAR Falko Schumann. All rights reserved. MIT license. */\n\n'
  }
}
